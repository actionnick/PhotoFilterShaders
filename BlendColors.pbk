<languageVersion : 1.0;>

kernel Blend
<   namespace : "BlendColors";
    vendor : "Paperless Post";
    version : 1;
>
{
    input image4 baseImg;
    output pixel4 result;
    
    
    //1 - darken
    //2 - multiply
    //3 - color burn
    //4 - linear burn
    //5 - lighten
    //6 - screen
    //7 - color dodge
    //8 - linear dodge
    //9 - overlay
    //10 - soft light
    //11 - hard light
    //12 - vivid light
    //13 - linear light
    //14 - pin light
    //15 - difference
    //16 - exclusion
    parameter int mode
    <
        minValue:int(0);
        maxValue:int(16);
        defaultValue:int(0);
    >;

    //0 - other color
    //1 - gradient
    //2 - image
    parameter int blendType
    <
        minValue:int(0);
        maxValue:int(2);
        defaultValue:int(0);
    >;
    
    // primary blending color
    parameter float3 color1
    <
        minValue:float3(0.0,0.0,0.0);
        maxValue:float3(1.0,1.0,1.0);
        defaultValue:float3(0.0,0.0,0.0);
    >;
    
    // secondary blending color
    parameter float3 color2
    <
        minValue:float3(0.0,0.0,0.0);
        maxValue:float3(1.0,1.0,1.0);
        defaultValue:float3(0.0,0.0,0.0);
    >;
    
    
    // gradient exponent control
    parameter float exponent
    <
        minValue:float(-10.0);
        maxValue:float(10.0);
        defaultValue:float(0.0);
    >;

    // gradient factor control
    parameter float factor
    <
        minValue:float(-10.0);
        maxValue:float(10.0);
        defaultValue:float(0.0);
    >;
    
    // image size
    parameter float4 bBox
    <
        minValue:float4(0.0,0.0,0.0,0.0);
        maxValue:float4(10000.0,10000.0,10000.0,10000.0);
        defaultValue:float4(0.0,600.0,0.0,400.0);
    >;

    // center of image
    parameter float2 center
    <
        minValue:float2(0.0,0.0);
        maxValue:float2(10000.0,10000.0);
        defaultValue:float2(300.0,200.0);
    >;
    
    // Photoshop blend modes
    // Math from: http://dunnbypaul.net/blends/
    void
    evaluatePixel()
    {
        float2 pos = outCoord();
        result = sampleNearest(baseImg,outCoord());
        float3 blend = float3(result.r,result.g,result.b);  // blendType = 2
    
        if(blendType == 0){ // blendType = 0
            blend = color1;
        }
        else if(blendType == 1){ // blendType = 1
            blend = color1;
            float dist = distance(center, outCoord());
            dist = dist/distance(center,float2(bBox[1],bBox[3]));      // normalize the distance between 0 and 1
            float colorUp = factor * pow(dist,exponent);                // used to colorize darken areas
            dist = 1.0 - factor * pow(dist,exponent);                   // darken colors in relation to distance from center
            
            blend.r = blend.r * dist;
            blend.r = blend.r + (colorUp * color2.r);
            blend.g = blend.g * dist;
            blend.g = blend.g + (colorUp * color2.g);
            blend.b = blend.b * dist;
            blend.b = blend.b + (colorUp * color2.b);
        }
        
        result = sampleNearest(baseImg,outCoord());
        float3 base = float3(result.r,result.g,result.b);
        float3 dst;
        
        if(mode == 1){          //darken
            dst.r = min(base.r,blend.r);
            dst.g = min(base.g,blend.g);
            dst.b = min(base.b,blend.b);
        }
        else if(mode == 2){     //multiply
            dst = base * blend;
        }
        else if(mode == 3){     //color burn
            dst = 1.0 - (1.0 - base)/blend;
        }
        else if(mode == 4){     //linear burn
            dst = base + blend - 1.0;
        }
        else if(mode == 5){     //lighten
            dst = max(base,blend);
        }
        else if(mode == 6){     //screen
            dst = 1.0 - (1.0 - base)*(1.0 - blend);
        }
        else if(mode == 7){     //color dodge
            dst = base/(1.0 - blend);
        }
        else if(mode == 8){     //linear dodge
            dst = base + blend;
        }
        else if(mode == 9){     //overlay
            //red
            if(base.r > 0.5){  
                dst.r = 1.0 - 2.0 * (1.0 - base.r) * (1.0 - blend.r);
            }
            else{
                dst.r = 2.0 * base.r * blend.r;
            }
            
            //green
            if(base.g > 0.5){
                dst.g = 1.0 - 2.0 * (1.0 - base.g) * (1.0 - blend.g);
            }
            else{
                dst.g = 2.0 * base.g * blend.g;
            }
            
            //blue
            if(base.b > 0.5){
                dst.b = 1.0 - 2.0 * (1.0 - base.b) * (1.0 - blend.b);
            }
            else{
                dst.b = 2.0 * base.b * blend.b;
            }
        }
        else if(mode == 10){    //soft light
            //red
            if(blend.r < 0.5){   
                dst.r = 2.0 * base.r * blend.r + base.r * base.r * (1.0 - 2.0 * blend.r);
            }
            else{
                dst.r = sqrt(base.r) * (2.0 * blend.r - 1.0) + 2.0 * base.r * (1.0 - blend.r);
            }
            
            //green
            if(blend.g < 0.5){   
                dst.g = 2.0 * base.g * blend.g + base.g * base.g * (1.0 - 2.0 * blend.g);
            }
            else{
                dst.g = sqrt(base.g) * (2.0 * blend.g - 1.0) + 2.0 * base.g * (1.0 - blend.g);
            }
            
            //blue
            if(blend.b < 0.5){   
                dst.b = 2.0 * base.b * blend.b + base.b * base.b * (1.0 - 2.0 * blend.b);
            }
            else{
                dst.b = sqrt(base.b) * (2.0 * blend.b - 1.0) + 2.0 * base.b * (1.0 - blend.b);
            }
        }
        else if(mode == 11){    //hard light
            //red
            if(blend.r > 0.5){  
                dst.r = 1.0 - 2.0 * (1.0 - blend.r) * (1.0 - base.r);
            }
            else{
                dst.r = 2.0 * blend.r * base.r;
            }
            
            //green
            if(blend.g > 0.5){  
                dst.g = 1.0 - 2.0 * (1.0 - blend.g) * (1.0 - base.g);
            }
            else{
                dst.g = 2.0 * blend.g * base.g;
            }
            
            //blue
            if(blend.b > 0.5){  
                dst.b = 1.0 - 2.0 * (1.0 - blend.b) * (1.0 - base.b);
            }
            else{
                dst.b = 2.0 * blend.b * base.b;
            }
        }
        else if(mode == 12){    //vivid light
            //red
            if(blend.r < 0.5){
                dst.r = 1.0 - (1.0 - base.r)/(2.0 * blend.r);
            }
            else{   
                dst.r = base.r/(1.0 - (2.0 * (blend.r - 0.5)));
            }
            
            //green
            if(blend.g < 0.5){
                dst.g = 1.0 - (1.0 - base.g)/(2.0 * blend.g);
            }
            else{   
                dst.g = base.g/(1.0 - (2.0 * (blend.g - 0.5)));
            }
            
            //blue
            if(blend.b < 0.5){
                dst.b = 1.0 - (1.0 - base.b)/(2.0 * blend.b);
            }
            else{   
                dst.b = base.b/(1.0 - (2.0 * (blend.b - 0.5)));
            }
        }
        else if(mode == 13){    //linear light
            //red
            if(blend.r < 0.5){
                dst.r = base.r + (2.0 * blend.r) - 1.0;
            }
            else{
                dst.r = base.r + (2.0 * (blend.r - 0.5));
            }
            
            //green
            if(blend.g < 0.5){
                dst.g = base.g + (2.0 * blend.g) - 1.0;
            }
            else{
                dst.g = base.g + (2.0 * (blend.g - 0.5));
            }
            
            //blue
            if(blend.b < 0.5){
                dst.b = base.b + (2.0 * blend.b) - 1.0;
            }
            else{
                dst.b = base.b + (2.0 * (blend.b - 0.5));
            }
        }
        else if(mode == 14){    //pin light
            //red
            if(blend.r < 0.5){
                dst.r = min(base.r,2.0 * blend.r);
            }
            else{
                dst.r = max(base.r,(2.0 *(blend.r - 0.5)));
            }
            
            //green
            if(blend.g < 0.5){
                dst.g = min(base.g,2.0 * blend.g);
            }
            else{
                dst.g = max(base.g,(2.0 *(blend.g - 0.5)));
            }
            
            //blue
            if(blend.b < 0.5){
                dst.b = min(base.b,2.0 * blend.b);
            }
            else{
                dst.b = max(base.b,(2.0 *(blend.b - 0.5)));
            }
        }
        else if(mode == 15){    //difference
            dst = abs(base - blend);
        }
        else if(mode == 16){    //exclusion
            dst = 0.5 - 2.0*(base - 0.5)*(blend - 0.5);;
        }
        else{   // normal
            float4 orig = sampleNearest(baseImg,outCoord());
            dst = float3(orig.r,orig.g,orig.b);
        }   
        
        result.r = dst.r;
        result.g = dst.g;
        result.b = dst.b;
    }
}
